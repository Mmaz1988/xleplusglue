DEMO ENGLISH MORPHOLOGY (1.0)

TOKENIZE:
P!tokenizers/basic-parse-tok.fst G!tokenizers/default-gen-tokenizer.fst

ANALYZE:
tokenizers/english.infl.patch.full.fst

----

MORPH   ENGLISH   RULES (1.0)

"sample rule"

"this deals with verbs.  
The sublexcial items are the POS assigned to the various tags in morph-lex.lfg" 

   V --> V-S_BASE "verb stem, e.g. run"
 	 V-POS_BASE "suffix saying that this is a verb: +Verb"
 	 {TNS_BASE "tense suffix, e.g. +Pres" 
 	   PERS_BASE "person suffix, e.g. +3sg"
 	 | ASP_BASE}.   "aspectual information"

  
   N --> N-S_BASE "noun stem"
 	 N-POS_BASE
 	 (N-NAME_BASE) "allow for +Giv or +Place in names"
 	 (N-GEND_BASE) "allow for gender information" 
 	 N-NUM_BASE.

   A --> ADJ-S_BASE
 	 ADJ-POS_BASE.
	 
   ADV --> ADV-S_BASE
 	  ADV-POS_BASE.

----
MORPH   ENGLISH   LEXICON (1.0)

"this guesses words that are unknown to your lexicon
 to be either adjectives or nouns"
-unknown  ADJ-S XLE @(PRED %stem);
	  ADV-S XLE @(PRED %stem);
	  N-S XLE @(PRED %stem)
	      (s::^ TYPE) = entity
		  { (^ NTYPE) = count
		    @(LLCLOSURE %mc822)
@(LLATOM %mc820 s::^ e)
@(LLATOM %mc821 s::^ t)
@(LLIMP %mc822 %mc820 %mc821)
@(CONCAT lam `( X `, drs `(  `[  `]  `,  `[ pred `( %stem `, X `)  `]  `)  `)  %mc823)
@(GLUE-MEANING %mc822 %mc823)		  |
		  (^ NTYPE) = name
		  @(NAME-SCOPE ^ %q)
		  @(LLCLOSURE %mc826)
@(LLATOM %mc824 s::^ e)
@(LLATOM %mc825 s::^ t)
@(LLIMP %mc826 %mc824 %mc825)
@(CONCAT lam `( X `, drs `(  `[  `]  `,  `[ eq `( X `,  `' %stem `'  `)  `]  `)  `)  %mc827)
@(GLUE-MEANING %mc826 %mc827)	      @(LLCLOSURE %mc836)
@(LLATOM %mc828 s::^ e)
@(LLATOM %mc829 s::^ t)
@(LLIMP %mc830 %mc828 %mc829)
@(LLATOM %mc831 s::^ e)
@(LLATOM %mc832 %q t)
@(LLIMP %mc833 %mc831 %mc832)
@(LLATOM %mc834 %q t)
@(LLIMP %mc835 %mc833 %mc834)
@(LLIMP %mc836 %mc830 %mc835)
@(CONCAT lam `( P `, lam `( Q `, merge `( drs `(  `[ X `]  `,  `[  `]  `)  `, merge `( app `( P `, X `)  `, app `( Q `, X `)  `)  `)  `)  `)  %mc837)
@(GLUE-MEANING %mc836 %mc837)
@(NOSCOPE %mc836)		  }.  


            "lam(Y,lam(P,lam(X,merge(drs([],[%stem(X,Y)]),app(P,X))))"

"lexical entries for tags coming out of the morphological analyzer" 

"verbal tags"

+Verb 	  V-POS XLE .

+Pres 	  TNS XLE @VPRES
		   @(PROG -_)
		   @(PERF -_).

+PastBoth TNS XLE "past tense or past particle"
		  { @VPAST | @PASTP }
		  @(PROG -_)
		  @(PERF -_).

+PastTense TNS XLE "past tense"
		   @VPAST
		   @(PROG -_)
		   @(PERF -_).

+PastPerf TNS XLE @PASTP.

+Prog 	  ASP XLE @VPROG.

+Inf 	  ASP XLE .

+3sg 	  PERS XLE @S-AGR.

+123SP 	  PERS XLE .

+Non3sg   PERS XLE @BARE-AGR.

"nominal/verbal"

+Pl 	  N-NUM XLE (^ NUM) = pl;
	  PERS XLE {(^ NUM) = sg |
	       	    (^ NUM) = pl}.

+Sg 	  N-NUM XLE (^ NUM) = sg
                    @(PERS 3);
	  PERS XLE (^ NUM) = sg.

"nominal tags"

+Noun 	  N-POS XLE @(NPOS count) "3rd person count noun"
		    @(PERS 3).
		       
+Prop 	  N-POS XLE @(NPOS name) "3rd person name"
		    @(PERS 3).   

+Giv 	  N-NAME XLE .   "for given names, no info associated so far"

+Fem 	  N-GEND XLE (^GEND) = fem. 

+Masc 	  N-GEND XLE (^GEND) = masc.


"other"

+Adj 	  ADJ-POS XLE .

+Adv 	  ADV-POS XLE .


"""
TODO: morphemes of be include '+sg', which doesn't seem to occur for verbs elsewhere (?) 
Needs fixing
"""

----
