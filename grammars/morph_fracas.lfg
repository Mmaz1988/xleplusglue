DEMO ENGLISH MORPHOLOGY (1.0)

TOKENIZE:
P!tokenizers/basic-parse-tok.fst G!tokenizers/default-gen-tokenizer.fst

ANALYZE:
tokenizers/english.infl.patch.full.fst

----

MORPH   ENGLISH   RULES (1.0)

"sample rule"

"this deals with verbs.  
The sublexcial items are the POS assigned to the various tags in morph-lex.lfg" 

   V --> V-S_BASE "verb stem, e.g. run"
 	 V-POS_BASE "suffix saying that this is a verb: +Verb"
 	 {TNS_BASE "tense suffix, e.g. +Pres" 
 	   PERS_BASE "person suffix, e.g. +3sg"
 	 | ASP_BASE}.   "aspectual information"

  
   N --> N-S_BASE "noun stem"
 	 N-POS_BASE
 	 (N-NAME_BASE) "allow for +Giv or +Place in names"
 	 (N-GEND_BASE) "allow for gender information" 
 	 N-NUM_BASE.

   A --> ADJ-S_BASE
 	 ADJ-POS_BASE.
	 
   ADV --> ADV-S_BASE
 	  ADV-POS_BASE.

----
MORPH   ENGLISH   LEXICON (1.0)

"this guesses words that are unknown to your lexicon
 to be either adjectives or nouns"
-unknown  ADJ-S XLE @(PRED %stem);
	  ADV-S XLE @(PRED %stem);
	  N-S XLE @(PRED %stem)
	      (s::^ TYPE) = entity
		  { (^ NTYPE) = count
		    @(LLCLOSURE %mc906)
@(LLATOM %mc904 s::^ e)
@(LLATOM %mc905 s::^ t)
@(LLIMP %mc906 %mc904 %mc905)
@(CONCAT lam `( X `, drs `(  `[  `]  `,  `[ pred `( %stem `, X `)  `]  `)  `)  %mc907)
@(GLUE-MEANING %mc906 %mc907)		  |
		  (^ NTYPE) = name
		  @(NAME-SCOPE ^ %q)
		  @(LLCLOSURE %mc910)
@(LLATOM %mc908 s::^ e)
@(LLATOM %mc909 s::^ t)
@(LLIMP %mc910 %mc908 %mc909)
@(CONCAT lam `( X `, drs `(  `[  `]  `,  `[ eq `( X `,  `' %stem `'  `)  `]  `)  `)  %mc911)
@(GLUE-MEANING %mc910 %mc911)	      @(LLCLOSURE %mc920)
@(LLATOM %mc912 s::^ e)
@(LLATOM %mc913 s::^ t)
@(LLIMP %mc914 %mc912 %mc913)
@(LLATOM %mc915 s::^ e)
@(LLATOM %mc916 %q t)
@(LLIMP %mc917 %mc915 %mc916)
@(LLATOM %mc918 %q t)
@(LLIMP %mc919 %mc917 %mc918)
@(LLIMP %mc920 %mc914 %mc919)
@(CONCAT lam `( P `, lam `( Q `, merge `( drs `(  `[ X `]  `,  `[  `]  `)  `, merge `( app `( P `, X `)  `, app `( Q `, X `)  `)  `)  `)  `)  %mc921)
@(GLUE-MEANING %mc920 %mc921)
@(NOSCOPE %mc920)		  }.  


            "lam(Y,lam(P,lam(X,merge(drs([],[%stem(X,Y)]),app(P,X))))"

"lexical entries for tags coming out of the morphological analyzer" 

"verbal tags"

+Verb 	  V-POS XLE .

+Pres 	  TNS XLE @VPRES
		   @(PROG -_)
		   @(PERF -_).

+PastBoth TNS XLE "past tense or past particle"
		  { @VPAST | @PASTP }
		  @(PROG -_)
		  @(PERF -_).

+PastTense TNS XLE "past tense"
		   @VPAST
		   @(PROG -_)
		   @(PERF -_).

+PastPerf TNS XLE @PASTP.

+Prog 	  ASP XLE @VPROG.

+Inf 	  ASP XLE .

+3sg 	  PERS XLE @S-AGR.

+123SP 	  PERS XLE .

+Non3sg   PERS XLE @BARE-AGR.

"nominal/verbal"

+Pl 	  N-NUM XLE (^ NUM) = pl;
	  PERS XLE {(^ NUM) = sg |
	       	    (^ NUM) = pl}.

+Sg 	  N-NUM XLE (^ NUM) = sg
                    @(PERS 3);
	  PERS XLE (^ NUM) = sg.

"nominal tags"

+Noun 	  N-POS XLE @(NPOS count) "3rd person count noun"
		    @(PERS 3).
		       
+Prop 	  N-POS XLE @(NPOS name) "3rd person name"
		    @(PERS 3).   

+Giv 	  N-NAME XLE .   "for given names, no info associated so far"

+Fem 	  N-GEND XLE (^GEND) = fem. 

+Masc 	  N-GEND XLE (^GEND) = masc.


"other"

+Adj 	  ADJ-POS XLE .

+Adv 	  ADV-POS XLE .


"""
TODO: morphemes of be include '+sg', which doesn't seem to occur for verbs elsewhere (?) 
Needs fixing
"""

----
