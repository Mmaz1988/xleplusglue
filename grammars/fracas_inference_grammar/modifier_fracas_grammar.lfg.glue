 MODIFIER ENGLISH RULES (1.0)

   AP[_type $ {attributive predicative}] -->

 	  (ADVComp: _type = predicative
		    ! $ (^ ADJUNCT))

 	  ADV*: ! $ (^ ADJUNCT);

 	  { A: ^=!
 	  | Num: ^=!
 	  }

	  "than-comparatives as adjuncts on the AP"
	  "type of comparative is handled in the lexicon"
	  "EX1: The tree is greater than the plant."
	  "EX2: The tree is (as) great (as the plant)."
 	  (CPComp: _type = predicative
		   { (^ OBL-COMP) = !
		   | (^ ADJUNCT) = !})

 	  (COMMA).


   "rule for comparison constructions"
   "EX: He is greater than the dog (is)."
   CPComp --> CComp
 	  { NP: (^ OBJ) = !
		(! CASE) = acc
 	   |
	    "for elliptical constructions."
	    "EX: He is greater than the dog is (great)."
 	    S: (^ OBJ) = !
 	  }.


   PP --> { P: ^ = ! "head of PP"
	       (! PTYPE) =c sem; "semantic type, Adjunct"
 	    NP: (^ OBJ) = !
		(! CASE) = acc
 	  | P: ^ = ! "head of PP"
	       (! PTYPE) =c nosem; "non-semantxic type, OBL"
 	    NP
 	  }.


   ADVP -->
 	  { VP: (^ SUBJ PRED) = 'pro'
		~[(^ TNS-ASP TENSE)]
		
 	   |

 	    (ADVComp: ! $ (^ ADJUNCT))

 	    ADV: ^=!;

	    "than-comparatives as adjuncts on the AP"
	    "type of comparative is handled in the lexicon"
	    "EX1: The tree is greater than the plant."
	    "EX2: The tree is (as) great (as the plant)."
 	    (CPComp: (^ OBL-COMP) = !)

 	    (COMMA)

 	  }.



   "aber who sollte ja eigentlich zum gleichen subject wie der main clause referen"
   "get features of the NP on the who somehow -> NUM und PERS"
   CPrel --> NP: (! PRON-TYPE) =c rel
		 (^ SUBJ) = !
		 ((ADJUNCT ^) PERS)  = (^SUBJ PERS)
		 ((ADJUNCT ^) NUM)  = (^SUBJ NUM)
		 "from Pargram: (^ TOPIC-REL {(OBJ)|(OBL-PART)} [SPEC (POSS)]*) = (^ PRON-REL)"
		 ;
 	  VP: (^ TNS-ASP TENSE) "require that sentences have tense"
	     (^ FIN) = +.

----