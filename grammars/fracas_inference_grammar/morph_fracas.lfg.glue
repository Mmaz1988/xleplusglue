DEMO ENGLISH MORPHOLOGY (1.0)

TOKENIZE:
P!tokenizers/basic-parse-tok.fst G!tokenizers/default-gen-tokenizer.fst

ANALYZE:
tokenizers/english.infl.patch.full.fst

#ANALYZE USEALL:
#P!tokenizers/english.infl.lower.fst P!tokenizers/english.infl.patch.full.fst


#mwe's must exist in the lexicon:
BuildMultiwordsFromLexicon:


BuildMultiwordsFromMorphology:


MULTIWORD:
P!tokenizers/multiwordcapmarks.fst

----

MORPH   ENGLISH   RULES (1.0)

"sample rule"

"this deals with verbs.  
The sublexcial items are the POS assigned to the various tags in morph-lex.lfg" 

   V[_form] -->
     V-S_BASE "verb stem, e.g. run"
 	 V-POS_BASE "suffix saying that this is a verb: +Verb"
 	 {TNS_BASE "tense suffix, e.g. +Pres"
 	   PERS_BASE "person suffix, e.g. +3sg"
 	 | ASP_BASE}.   "aspectual information"

  
   N --> N-S_BASE "noun stem"
 	 N-POS_BASE
 	 (N-NAME_BASE) "allow for +Giv or +Place in names"
 	 (N-GEND_BASE) "allow for gender information" 
 	 N-NUM_BASE.

   A --> ADJ-S_BASE
 	 ADJ-POS_BASE
 	 (GRAD_BASE).
	 
   ADV --> ADV-S_BASE
 	  ADV-POS_BASE.

    NUMBER -->  NUMBER-S_BASE
                NUMBER-POS_BASE
                NUMBER-SPELL_BASE
                NUMBER-TYPE_BASE
                NUMBER-SIGN_BASE.
----
MORPH   ENGLISH   LEXICON (1.0)

"this guesses words that are unknown to your lexicon
 to be either adjectives or nouns"
-unknown  ADJ-S XLE @(PRED %stem);
          NUMBER-S XLE { @(NUMBER-PL %stem pl)
                       | @(NUMBER-PART %stem)};
	  ADV-S XLE @(PRED %stem);
	  N-S XLE @(PRED %stem)
	      (s::^ TYPE) = entity
		  { (^ NTYPE) = count
		    :$ lam(X,drs([],[pred(%stem,X)])) : (s::^_e -o s::^_t),
		  |
		  (^ NTYPE) = name
		  @(NAME-SCOPE ^ %q)
		  :$ lam(X,drs([],[eq(X,'%stem')])) : (s::^_e -o s::^_t),
	      :$ lam(P,lam(Q,merge(drs([X],[]),merge(app(P,X),app(Q,X))))) :
	      ((s::^_e -o s::^_t) -o ((s::^_e -o %q_t) -o %q_t)) || noscope,
		  }.  


            "lam(Y,lam(P,lam(X,merge(drs([],[%stem(X,Y)]),app(P,X))))"

"lexical entries for tags coming out of the morphological analyzer" 

"verbal tags"

+Verb 	  V-POS XLE .

+Pres 	  TNS XLE @VPRES
		   @(PROG -_)
		   @(PERF -_).

+PastBoth TNS XLE "past tense or past particle"
		  { @VPAST
		   (^ VFORM) = fin
		   | @PASTP
		    (^ VFORM) = nonfin}
		  @(PROG -_)
		  @(PERF -_).

+PastTense TNS XLE "past tense"
		   @VPAST
		   (^ VFORM) = fin
		   @(PROG -_)
		   @(PERF -_).

+PastPerf TNS XLE @PASTP.

+Prog 	  ASP XLE @VPROG.

+Inf 	  ASP XLE .

+3sg 	  PERS XLE @S-AGR
                   (^ VFORM) = fin.

+123SP 	  PERS XLE .

+Non3sg   PERS XLE @BARE-AGR
                   {(^ VFORM) = inf |
                    (^ VFORM) = fin }.

"nominal/verbal"

+Pl 	  N-NUM XLE (^ NUM) = pl.

+Sg 	  N-NUM XLE (^ NUM) = sg
                    @(PERS 3).

"nominal tags"

+Noun 	  N-POS XLE @(NPOS count) "3rd person count noun"
		    @(PERS 3).
		       
+Prop 	  N-POS XLE @(NPOS name) "3rd person name"
		    @(PERS 3).   

+Giv 	  N-NAME XLE .   "for given names, no info associated so far"

+Fem 	  N-GEND XLE (^GEND) = fem. 

+Masc 	  N-GEND XLE (^GEND) = masc.


"other"

+Adj 	  ADJ-POS XLE  { @ADJ-INT |
                        (^ SEMTYPE) =c degree
                        {@ADJ-POS |
                        ~[(^ DEGREE) = positive]}
                        }.

+Comp     GRAD XLE @(ADJ-COMP).

+Sup      GRAD XLE @(ADJ-SUP).

+Adv 	  ADV-POS XLE .



+Num      NUMBER-POS  XLE   .

+Dig       NUMBER-SPELL XLE .

+Spelled    NUMBER-SPELL XLE .

+Integer   NUMBER-TYPE XLE .

+PosNum     NUMBER-SIGN XLE .

"""
TODO: morphemes of be include '+sg', which doesn't seem to occur for verbs elsewhere (?) 
Needs fixing
"""

----
