VERB ENGLISH RULES (1.0)

   VP --> (AUX) "auxiliary introduced for passive"

      (NEG: ! $ (^ ADJUNCT))

 	  {V: ^=!
 	  | AUX: ^=!
 	         (! VFORM) =c modal  "eg. can"}

 	  ({ NP: (^ OBJ)=! "direct object"
		 (! CASE)=acc |
 	     NP: (^ PREDLINK) = ! "to be + ..."
 	   })
 	  (NP: (^ OBJ2) = !)

 	  (AP[predicative]: (^ PREDLINK) = !)

 	  (ADVP: ! $ (^ ADJUNCT)) "place adverb before non-argument PPs eg. All Europeans travel freely within Europe"

	  "secondary object"
 	  PP*: { ! $ (^ ADJUNCT) "PPs can be all types of adjuncts"
		 (! PTYPE) =c sem "only semantic PPs here"
		 "@(OT-MARK PPadj)" "disprefer adjunct PPs"
	       | (^ OBL) = ! "or a a general oblique"
	       };

 	  (PP: (^ OBL-TO) = ! "special obl for ditransitives"
	       "example of a constraining equation: require a value with =c"
	       (! PFORM) =c to )
 	  (PP: (^ OBL-AG) = ! "oblique agent of passive"
	       "example of a constraining equation: require a value with =c"
	       (! PFORM) =c by
	       @(OT-MARK PPpassiv))
 	  (
 	    {
 	     CP: (^ COMP) = !
 	    |
 	     VPinf: (^XCOMP) = !
		    (! FIN) = -
		    (! VFORM) = inf
		    "(s::^ TEMP-REF) = (s::(^XCOMP) EVAL)"}
 	   )

 	  (ADVP: ! $ (^ ADJUNCT)).

	  "rule for phrasal comparative"
	  "EX: He laughs greater than her."
	  "somewhat preliminary, should not be 'predicative', works for the moment though"
 	  "(AP[predicative]: ! $ (^ ADJUNCT))."

   VPinf --> {PARTinf VP
             | VP: ( (XCOMP ^) VFORM) =c modal }. "eg. can appear"



----